<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   applicationComplete="initApp()"
					   height="300" xmlns:local="*">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.containers.TitleWindow;
			import mx.controls.Alert;
			import mx.core.IFlexDisplayObject;
			import mx.managers.PopUpManager;
			
			import spark.components.Window;
			
			private var questions:ArrayCollection;
			private var currentExam:ArrayCollection;
			[Bindable]
			private var currentIndexQuestion:int = 0;
			private var currentQuestion:Question;
			private var time:Timer;
			
			private function initApp():void{
				loadQuestions();
				updateNumbers();
			}
			
			private function randomInRange(min:Number, max:Number):int {
				var scale:Number = max - min;
				return int(Math.random() * scale + min);
			}
			
			private function generateExam():void{
				//duplicates questions list
				var dupQuestions:ArrayCollection= new ArrayCollection();
				dupQuestions.addAll(questions);
				cleanAllQuestions(dupQuestions);
				//reset current Exam
				currentExam = new ArrayCollection();
				while (currentExam.length<howMuchQuestions.value){
					//takes a random item from dupQuestion
					var newRandom:int = randomInRange(0,dupQuestions.length-1);
					//Add it to current exam
					currentExam.addItem(dupQuestions.removeItemAt(newRandom));
				}
			}
			
			private function cleanAllQuestions(list:ArrayCollection):void{
				for each (var q:Question in list){
					q.userAnswer = null;
				}
			}
			
			private function updateNumbers():void{
				questionQuantity.text = questions.length.toString();
				howMuchQuestions.maximum = questions.length;
				howMuchQuestions.value = howMuchQuestions.maximum;
			}
			
			private function loadQuestions():void{
				questions = new ArrayCollection();
				var file:File = File.applicationDirectory;
				file = file.resolvePath("VoFs.txt");
				var fileStream:FileStream = new FileStream();
				fileStream.open(file, FileMode.READ);
				var bytesNumber:uint = fileStream.bytesAvailable;
				var allQuestions:String = "";
				while (bytesNumber>0){
					allQuestions += fileStream.readUTFBytes(bytesNumber);
					bytesNumber = fileStream.bytesAvailable;
				}
				//parse allQuestions
				for each (var token:String in allQuestions.split('\r\n')){
					var components:Array = token.split('#');
					if(components.length >=2){
						var question:Question = new Question(components[0]);
						var ans:String = components[1];
						if(ans.substr(0,1).toLowerCase()=='f'){
							question.answer = false;
						}else{
							question.answer = true;
						}
						if (components.length>2){
							question.clarification= components[2];
						}
						questions.addItem(question);
					}
				}
				fileStream.close();
			}

			protected function startExam(event:MouseEvent):void{
				//buttons...
				nextButton.enabled=true;
				beginButton.enabled=false;
				endButton.enabled=true;
				//time
				clock.start();
				//exam
				generateExam();
				currentIndexQuestion = -1;
				goToNextQuestion();
				
			}

			private function goToNextQuestion():void{
				//save First user answer
				saveCurrentAnswer();
				//reset true and false
				userAnswer.selectedValue=null;
				Verdadera.selected=false;
				Falsa.selected=false;
				//next
				if(currentExam.length > currentIndexQuestion+1){
					currentIndexQuestion++;
					currentQuestion = currentExam.getItemAt(currentIndexQuestion) as Question;
					currentQuestionText.text = currentQuestion.questionText;
				}else{ //FINISH
					nextButton.enabled=false;
					clock.stop();
				}
				
			}
			
			protected function nextButton_clickHandler(event:MouseEvent):void{
				goToNextQuestion();
			}


			private function saveCurrentAnswer():void{
				if(currentIndexQuestion!=-1){
					if(userAnswer.selectedValue!=null){
						currentQuestion.userAnswer = userAnswer.selectedValue.toString();
					}else{
						currentQuestion.userAnswer = null;
					}
				}
			}
			
			protected function endButton_clickHandler(event:Event):void	{
				//premature finish
				if(currentExam.length > currentIndexQuestion+1){
					saveCurrentAnswer();
					clock.stop();
				}
				beginButton.enabled = true;
				endButton.enabled = false;
				nextButton.enabled = false;
				ResultView.resultList = currentExam;
				var r:Window = new ResultView();
				r.open();
			}

		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
		<s:RadioButtonGroup id="userAnswer" />
	</fx:Declarations>
	<s:VGroup width="100%" height="100%">
		<s:VGroup  width="100%" verticalAlign="top" height="10%" paddingLeft="10" paddingTop="10" horizontalAlign="center">
			<s:HGroup>
				<s:Label text="Número de preguntas cargadas: "/>
				<s:Label id="questionQuantity" text="0"/>	
			</s:HGroup>
			<s:HGroup verticalAlign="middle">
				<s:Label text="Cuántas deseas hacer?"/>
				<s:NumericStepper id="howMuchQuestions" minimum="0" maximum="0" value="0" />	
			</s:HGroup>
			<s:HGroup>
				<s:Button id="beginButton" label="Comenzar!" click="startExam(event)"/>	
			</s:HGroup>
		</s:VGroup>
		<mx:Spacer height="10"/>
		<mx:HRule width="100%"/>
		<s:VGroup width="100%" verticalAlign="middle" height="80%">
			<s:HGroup width="100%" height="10%" verticalAlign="top">
				<s:HGroup width="80%" horizontalAlign="left">
					<s:Label fontWeight="bold" text="Pregunta: Nº" verticalAlign="middle"/>
					<s:Label text="{currentIndexQuestion+1}" verticalAlign="middle"/>
				</s:HGroup>
				<s:HGroup width="20%" horizontalAlign="right">
					<local:SimpleClock fontWeight="bold" id="clock"  width="100%" textAlign="right"/>
				</s:HGroup>
			</s:HGroup>
			<s:HGroup horizontalAlign="center" width="100%" height="80%" verticalAlign="middle">
				<s:TextArea id="currentQuestionText" width="100%" height="100%" editable="false"/>
			</s:HGroup>
			<s:HGroup width="90%" horizontalAlign="center" height="10%" verticalAlign="bottom">
				<s:RadioButton groupName="userAnswer" id="Verdadera" value="true" label="Verdadera" width="150"/>
				<s:RadioButton groupName="userAnswer" id="Falsa" value="false" label="Falsa" width="150"/>
				<s:Button id="nextButton" label="Siguiente" click="nextButton_clickHandler(event)" enabled="false"/>
			</s:HGroup>
		</s:VGroup>
		<mx:Spacer height="10"/>
		<mx:HRule width="100%"/>
		<s:VGroup verticalAlign="bottom" height="10%" horizontalAlign="center" width="100%">
			<s:Button id="endButton" label="Finalizar Examen" click="endButton_clickHandler(event)" enabled="false"/>	
		</s:VGroup>
	</s:VGroup>
</s:WindowedApplication>
